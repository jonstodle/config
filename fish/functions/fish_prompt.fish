function fish_prompt
	
    # - green lines if the last return command is OK, red otherwise
    # - your user name, in red if root or yellow otherwise
    # - your hostname, in cyan if ssh or blue otherwise
    # - the current path (with prompt_pwd)
    # - date +%X
    # - the current virtual environment, if any
    # - the current git status, if any, with __fish_git_prompt
    # - the current battery state, if any, and if your power cable is unplugged, and if you have "acpi"
    # - current background jobs, if any

    # It goes from:
    # â”¬â”€[nim@Hattori:~]â”€[11:39:00]
    # â•°â”€>$ echo here

    # To:
    # â”¬â”€[nim@Hattori:~/w/dashboard]â”€[11:37:14]â”€[V:django20]â”€[G:masterâ†‘1|â—1âœš1â€¦1]â”€[B:85%, 05:41:42 remaining]
    # â”‚ 2	15054	0%	arrÃªtÃ©e	sleep 100000
    # â”‚ 1	15048	0%	arrÃªtÃ©e	sleep 100000
    # â•°â”€>$ echo there
    set -l last_status $status

    set -q __fish_git_prompt_showupstream
    or set -g __fish_git_prompt_showupstream auto

    function _nim_prompt_wrapper
        set retc $argv[1]
        set field_name $argv[2]
        set field_value $argv[3]

        set_color normal
        set_color $retc
        echo -n 'â”€'
        set_color -o green
        echo -n '['
        set_color normal
        test -n $field_name
        and echo -n $field_name:
        set_color $retc
        echo -n $field_value
        set_color -o green
        echo -n ']'
    end

    test $last_status -eq 0
    and set retc green
    or set retc red

    set_color $retc
    echo -n 'â”¬â”€'
    set_color -o green
    echo -n '['
    set_color normal
    set_color green
    echo -n (prompt_pwd)
    set_color -o green
    echo -n ']'

    # Date
    _nim_prompt_wrapper $retc '' (date +%X)

    # Virtual Environment
    set -q VIRTUAL_ENV
    and _nim_prompt_wrapper $retc V (basename "$VIRTUAL_ENV")

    # git
    set prompt_git (__fish_git_prompt | string trim -c ' ()')
    test -n "$prompt_git"
    and _nim_prompt_wrapper $retc '' $prompt_git

    # Battery status
    type -q acpi
    and test (acpi -a 2> /dev/null | string match -r off)
    and _nim_prompt_wrapper $retc B (acpi -b | cut -d' ' -f 4-)

    _nim_prompt_wrapper $retc 'ðŸ”‹' (pmset -g ps | string match -r "[0-9]{0,2}[0-9]%")

    # New line
    echo

    # Background jobs
    set_color normal
    for job in (jobs)
        set_color $retc
        echo -n 'â”‚ '
        set_color brown
        echo $job
    end
    set_color normal
    set_color $retc
    echo -n 'â•°â”€> '
    set_color normal
end
